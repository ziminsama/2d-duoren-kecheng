class_name UpgradeOption
extends Node2D

signal selected(index: int, for_peer_it: int)

@onready var health_component: HealthComponent = $HealthComponent
@onready var hurtbox_component: HurtboxComponent = $HurtboxComponent
@onready var animation_player: AnimationPlayer = $AnimationPlayer
@onready var hit_flash_sprite_component: Sprite2D = $HitFlashSpriteComponent

var impact_particles_scene: PackedScene = preload("uid://cq1qm2s7qhrmo")
var ground_particles_scene: PackedScene = preload("uid://by4v06jb7ah4e")

var upgrade_index: int
var assigned_resource: UpgradeResource
var peer_id_filter: int = -1


func _ready() -> void:
	set_peer_id_filter(peer_id_filter)
	hurtbox_component.hit_by_hitbox.connect(_on_hit_by_bitbox)
	health_component.died.connect(_on_died)
	
	if is_multiplayer_authority():
		multiplayer.peer_disconnected.connect(_on_peer_disconnected)


func play_in(delay: float = 0):
	hit_flash_sprite_component.scale = Vector2.ZERO
	#两种方法await get_tree().create_timer(delay).timeout再触发,但是这样容易出问题,还是改成用tween更安全.我理解tween只是动画,东西是在的
	var tween := create_tween()
	tween.tween_interval(delay)
	tween.tween_callback(func ():
		animation_player.play("spawn")
	)


func set_peer_id_filter(new_peer_id: int):
	peer_id_filter = new_peer_id
	hurtbox_component.peer_id_filter = peer_id_filter
	hit_flash_sprite_component.peer_id_filter = peer_id_filter


func set_upgrade_index(index: int):
	upgrade_index = index


func set_upgrade_resource(upgrade_resource: UpgradeResource):
	assigned_resource = upgrade_resource


func kill():
	spawn_death_particles()
	queue_free()


func spawn_death_particles():
	var death_particles: Node2D = ground_particles_scene.instantiate()
	
	var background_node: Node = Main.background_mask
	if !is_instance_valid(background_node):
		#保险措施,防止节点不存在
		background_node = get_parent()
	
	background_node.add_child(death_particles)
	death_particles.global_position = global_position


func despawn():
	animation_player.play("despawn")


@rpc("authority", "call_local", "unreliable")
func spawn_hit_particles():
	var hit_particles: Node2D = impact_particles_scene.instantiate()
	hit_particles.global_position = hurtbox_component.global_position
	get_parent().add_child(hit_particles)


@rpc("authority", "call_local", "reliable")
func kill_all(killed_name: String):
	var upgrade_option_nodes := get_tree().get_nodes_in_group("upgrade_option")
	
	for upgrade_option in upgrade_option_nodes:
		if upgrade_option.peer_id_filter == peer_id_filter:
			if upgrade_option.name == killed_name:
				upgrade_option.kill()
			else:
				upgrade_option.despawn()


func _on_died():
	selected.emit(upgrade_index, peer_id_filter)
	kill_all.rpc_id(MultiplayerPeer.TARGET_PEER_SERVER, name)
	#if is_multiplayer_authority(): 我是用这个,教学用下面那个
	if peer_id_filter != MultiplayerPeer.TARGET_PEER_SERVER:
		kill_all.rpc_id(peer_id_filter, name)


func _on_peer_disconnected(peer_id: int):
	if peer_id == peer_id_filter:
		despawn()


func _on_hit_by_bitbox():
	spawn_hit_particles.rpc_id(peer_id_filter)
